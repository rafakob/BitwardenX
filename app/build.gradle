apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"

android {
    compileSdkVersion(28)

    defaultConfig {
        applicationId = "com.rafakob.bitwarden"
        minSdkVersion(21)
        targetSdkVersion(28)
        versionCode = 1
        versionName = "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        debug.java.srcDirs += 'src/debug/kotlin'
        mock.java.srcDirs += 'src/mock/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    signingConfigs {
        release {
            storeFile file('bitwarden-debug.jks')
            keyAlias 'bitwarden'
            keyPassword 'bitwarden'
            storePassword 'bitwarden'
        }
    }

    buildTypes {
        release {
            debuggable false
            signingConfig signingConfigs.release
            postprocessing {
                removeUnusedCode true
                removeUnusedResources true
                obfuscate true
                optimizeCode true
                proguardFiles 'proguard-rules.pro', 'proguard-rules-test.pro'
            }
        }
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.release
        }
        mock {
            debuggable true
            applicationIdSuffix ".mock"
            signingConfig signingConfigs.release
            matchingFallbacks = ['debug']
        }
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    testBuildType "mock"
}

dependencies {
    implementation project(":base")
    implementation project(":resources")
    implementation project(':feature-splash')
    implementation project(':feature-startup')
    implementation project(':service-crypto')
    implementation project(':service-environment')

    implementation(Deps.kotlin)
    implementation(Deps.androidAppCompat)
    implementation(Deps.androidCoreKtx)
    implementation(Deps.materialComponents)
    implementation(Deps.constraintLayout)
    implementation(Deps.daggerAndroid)
    implementation(Deps.daggerAndroidSupport)
    implementation(Deps.timber)

    kapt(Deps.daggerCompiler)
    kapt(Deps.daggerAndroidProcessor)

    testImplementation(TestDeps.junit)

    androidTestImplementation(TestDeps.espressoRunner)
    androidTestImplementation(TestDeps.espressoCore)
    androidTestImplementation(TestDeps.espressoRules)

    androidTestUtil(TestDeps.orchestrator)
}